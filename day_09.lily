#[
    Advent of Code 2025
    Day 09: Movie Theater
]#

import (abs) math

var tiles_x: List[Integer] = []
var tiles_y: List[Integer] = []
var tile_count = 0

for line in File.read_to_string("inputs/day_09.txt").split("\n"): {
    var parts = line.split(",")
    tiles_x.push(parts[0].parse_i().unwrap())
    tiles_y.push(parts[1].parse_i().unwrap())
    tile_count += 1
}

var lines = List.fill(tile_count, (|index|
    var next_index = index + 1
    if next_index == tile_count: {
        next_index = 0
    }

    var x1 = tiles_x[index]
    var y1 = tiles_y[index]
    var x2 = tiles_x[next_index]
    var y2 = tiles_y[next_index]

    if x1 == x2: {
        return (y1 <= y2 ? <[x1, x2, y1, y2]> : <[x1, x2, y2, y1]>)
    }

    (x1 < x2 ? <[x1, x2, y1, y2]> : <[x2, x1, y1, y2]>)
))

var max_index = tile_count - 1

var largest_area_all_tiles = 0
var largest_area_red_and_green_tiles = 0

for i in 0...max_index: {
    for j in i + 1...max_index: {
        var x1 = tiles_x[i]
        var y1 = tiles_y[i]
        var x2 = tiles_x[j]
        var y2 = tiles_y[j]

        var area = (abs(x2 - x1) + 1) * (abs(y2 - y1) + 1)

        if area > largest_area_all_tiles: {
            largest_area_all_tiles = area
        }

        if area <= largest_area_red_and_green_tiles: {
            continue
        }

        if x1 > x2: {
            var temp = x1
            x1 = x2
            x2 = temp
        }

        if y1 > y2: {
            var temp = y1
            y1 = y2
            y2 = temp
        }

        var inside = true
        for line in lines: {
            if line[1] > x1 && line[0] < x2 && line[3] > y1 && line[2] < y2: {
                inside = false
                break
            }
        }

        if inside: {
            largest_area_red_and_green_tiles = area
        }
    }
}

print("Part 1: {}\nPart 2: {}".format(
    largest_area_all_tiles, largest_area_red_and_green_tiles
))
