#[
    Advent of Code 2025
    Day 06: Trash Compactor
]#

define add(sum: Integer, number: Integer): Integer {
    return sum + number
}

define multiply(product: Integer, number: Integer): Integer {
    return product * number
}

var lines = File.read_to_string("inputs/day_06.txt").split("\n")
var number_rows = lines.size() - 1

var numbers_horizontal: List[Integer] = List.repeat(number_rows, 0)
var factors_horizontal: List[Integer] = List.repeat(number_rows, 1)
var numbers_vertical: List[Integer] = []

var total_horizontal = 0
var total_vertical = 0

var skip_column = false
for column in lines[0].size() - 1...0 by -1: {
    if skip_column: {
        skip_column = false
        continue
    }

    var number_vertical = 0
    for i, number_horizontal in numbers_horizontal: {
        var char = lines[i][column]
        if char != ' ': {
            var digit = char - '0'
            numbers_horizontal[i] += digit * factors_horizontal[i]
            factors_horizontal[i] *= 10
            number_vertical = number_vertical * 10 + digit
        }
    }

    numbers_vertical.push(number_vertical)

    var char = lines[number_rows][column]
    if char == '+': {
        total_horizontal += numbers_horizontal.fold(0, add)
        total_vertical += numbers_vertical.fold(0, add)
    elif char == '*':
        total_horizontal += numbers_horizontal.fold(1, multiply)
        total_vertical += numbers_vertical.fold(1, multiply)
    else:
        continue
    }

    for i in 0...number_rows - 1: {
        numbers_horizontal[i] = 0
        factors_horizontal[i] = 1
    }

    numbers_vertical.clear()
    skip_column = true
}

print("Part 1: {}\nPart 2: {}".format(total_horizontal, total_vertical))
