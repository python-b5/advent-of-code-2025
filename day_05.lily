#[
    Advent of Code 2025
    Day 05: Cafeteria
]#

var line_groups = File
    .read_to_string("inputs/day_05.txt")
    .split("\n\n")
    .map(|string| string.split("\n"))

var ranges = line_groups[0].map(|line|
    var endpoints = line.split("-")
    <[endpoints[0].parse_i().unwrap(), endpoints[1].parse_i().unwrap()]>
).sort(|a, b| a[0] - b[0])

var ids = line_groups[1].map(String.parse_i).map(Option.unwrap)

var fresh_ids = 0
for id in ids: {
    for range in ranges: {
        if id >= range[0] && id <= range[1]: {
            fresh_ids += 1
            break
        }
    }
}

print("Part 1: {}".format(fresh_ids))

var i = 1
while i < ranges.size(): {
    var current = ranges[i]
    var previous = ranges[i - 1]

    if current[0] <= previous[1]: {
        if current[1] > previous[1]: {
            previous[1] = current[1]
        }

        ranges.delete_at(i)
        continue
    }

    i += 1
}

var ids_in_ranges = 0
for range in ranges: {
    ids_in_ranges += range[1] - range[0] + 1
}

print("Part 2: {}".format(ids_in_ranges))
