#[
    Advent of Code 2025
    Day 05: Cafeteria
]#

var line_groups = File
    .read_to_string("inputs/day_05.txt")
    .split("\n\n")
    .map(|string| string.split("\n"))

var ranges = line_groups[0].map(|line|
    var endpoints = line.split("-")
    <[endpoints[0].parse_i().unwrap(), endpoints[1].parse_i().unwrap()]>
).sort(|a, b| a[0] - b[0])

print("Part 1: {}\nPart 2: {}".format(
    line_groups[1]
        .map(String.parse_i)
        .map(Option.unwrap)
        .count(|id| ranges.any(|range| id >= range[0] && id <= range[1])),
    ranges
        .accumulate([].@(List[Tuple[Integer, Integer]]), (|merged_ranges, range|
            if merged_ranges: {
                var previous = merged_ranges[-1]
                if range[0] <= previous[1]: {
                    if range[1] > previous[1]: {
                        previous[1] = range[1]
                    }
                else:
                    merged_ranges.push(range)
                }
            else:
                merged_ranges.push(range)
            }
        ))
        .fold(0, (|sum, range| sum + range[1] - range[0] + 1))
))
